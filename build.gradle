plugins {
    id 'fabric-loom' version '1.14.10'
    id 'maven-publish'
}

version = project.version
group = project.group

base {
    archivesName = "${mod_name}-${minecraft_version}"
}

repositories {
    mavenCentral()
    maven { url 'https://maven.shedaniel.me/' }
    maven { url 'https://maven.terraformersmc.com/' }
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"

    modApi("me.shedaniel.cloth:cloth-config-fabric:${cloth_config_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }
    modImplementation "com.terraformersmc:modmenu:${mod_menu_version}"
}

processResources {
    inputs.property "version", project.version
    inputs.property "minecraft_version", project.minecraft_version
    inputs.property "loader_version", project.fabric_loader_version

    filesMatching("fabric.mod.json") {
        expand "version": project.version,
                "minecraft_version": project.minecraft_version,
                "loader_version": project.fabric_loader_version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.release = 21
}

java {
    withSourcesJar()
    sourceCompatibility = 21
    targetCompatibility = 21
}

jar {
    from("LICENSE") {
        rename { "${it}_${mod_name}" }
    }
}

loom {
    mixin {
        defaultRefmapName = "voxyworldgenv2.refmap.json"
    }
}
